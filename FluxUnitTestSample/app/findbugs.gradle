apply plugin: 'findbugs'

project.afterEvaluate {
  android.applicationVariants.all { variant ->
    task "findbugs${variant.name.capitalize()}"(type: FindBugs, dependsOn: "assemble${variant.name.capitalize()}") {
      description "Analyze ${variant.name} code with the findbugs tool"
      group "Verification"

      ignoreFailures = false
      effort = "default"
      reportLevel = "medium"

      classes = files("$project.buildDir/intermediates/classes/${variant.name}")
      excludeFilter = file("findbugs.xml")
      source = variant.javaCompile.source
      classpath = variant.javaCompile.classpath

      reports {
        // Only one of HTML or XML can be turned on at the same time
        html.enabled = true
        xml.enabled = !html.enabled
        xml.withMessages = true

        html.destination = "$project.buildDir/reports/findbugs/findbugs-${variant.name}-output.html"
        xml.destination = "$project.buildDir/reports/findbugs/findbugs-${variant.name}-output.xml"
      }

      dependsOn "compile${variant.name.capitalize()}JavaWithJavac"
    }
  }
}